// AFX: "AFXCLIP" "0..1"
// AFX: "AFXDEPTHMODE" "0..4"
// AFX: "AFXD24" "0..2"

// SKIP: $AFXD24 == 2 && ($AFXDEPTHMODE != 0)

// AFXCLIP:
// 0 - no clipping
// 1 - clip max value (so we don't overdraw previous depth-passes (i.e. skybox)).

// AFXDEPTHMODE:
// 0 - inverse depth (original)
// 1 - linear depth (with spherical correction)
// 2 - logarithmic depth (with spherical correction) (natural logarithm)
// 3 - linear depth
// 4 - logarithmic depth (natural logarithm)

// AFXD24:
// 0 - draw gray depth
// 1 - draw rgb-encoded depth
// 2 - rgba pack (for afx_interop)

// AFXSPHERICAL:
// 0 - no correction
// 1 - convert pyramidal depth buffer to spherical depth

const float4 g_AfxConstants : register( c5 );

#define g_Afx_zNear g_AfxConstants.x
#define g_Afx_zFar g_AfxConstants.y
#define g_Afx_OutNear g_AfxConstants.z
#define g_Afx_OutFar g_AfxConstants.w

#if 1 == AFXDEPTHMODE || 2 == AFXDEPTHMODE
const float4x4 g_AfxInvProjection : register( c6 );
#endif

sampler g_sDepthTextureSampler : register( s0 );

struct PS_INPUT
{
	float2 t0 : TEXCOORD0;
};

float4 main( PS_INPUT i ) : COLOR
{
	float depth = tex2D(g_sDepthTextureSampler, i.t0).x;
	
#if 1 == AFXCLIP
	clip(depth >= 1 ? -1 : 1);
#endif

#if 0 == AFXDEPTHMODE

	float f1 = (-1) * g_Afx_zFar * g_Afx_zNear * 1.0;
	float xD = g_Afx_zFar - g_Afx_zNear;

	float outNear = (f1 / g_Afx_OutNear + g_Afx_zFar)/xD;
	float outFar = (f1 / g_Afx_OutFar + g_Afx_zFar)/xD;
	
	depth = (depth - outNear) / (outFar - outNear);

#elif 1 == AFXDEPTHMODE || 2 == AFXDEPTHMODE

	// Linearize Quake-like depth buffer with spherical correction using the inverse projection matrix:

	float4 eyeVec = mul(g_AfxInvProjection, float4(2 * i.t0.x - 1, 2 * i.t0.y - 1, depth, 1.0));
	
	eyeVec.xyz /= eyeVec.w;
	
	depth = length(eyeVec.xyz);
	
	// Transform [outNear,OutFar] to [0,1]
	
	depth = (depth - g_Afx_OutNear) / (g_Afx_OutFar - g_Afx_OutNear);
	
#if 2 == AFXDEPTHMODE
	
	// Logarithmize depth buffer:
	
	depth = log(1 + depth);
#endif

#elif 3 == AFXDEPTHMODE || 4 == AFXDEPTHMODE

	float f1 = (-1) * g_Afx_zFar * g_Afx_zNear * 1.0;
	float xD = g_Afx_zFar - g_Afx_zNear;

	// Linearize Quake-like depth buffer:

	depth = f1/(depth * xD -g_Afx_zFar);
	
	// Transform [outNear,OutFar] to [0,1]
	
	depth = (depth - g_Afx_OutNear) / (g_Afx_OutFar - g_Afx_OutNear);
	
#if 4 == AFXDEPTHMODE
	
	// Logarithmize depth buffer:
	
	depth = log(1 + depth);
#endif

#endif


#if 0 == AFXD24
	return float4(depth, depth, depth, 1.0);
#elif 1 == AFXD24
	depth = saturate(depth);
	const float maxint24 = 256*256*256-1;
	float3 color = depth * float3(maxint24/(256),maxint24/(256*256),maxint24/(256*256*256));
	color = frac(color);
	color.b -= color.g/256.0;
	color.g -= color.r/256.0;
	color *= 256/255.0;
	return float4(color, 1.0);
#elif 2 == AFXD24
	if(depth < 0) return float4(0,0,0,0);
	if(depth >= 1) return float4(1,1,1,1);
	
	const float4 kEncodeMul = float4(1.0, 255.0, 65025.0, 16581375.0);
	const float4 kEncodeBit = 1.0/255.0;
	
    float4 enc = kEncodeMul * depth;
    enc = frac (enc);
    enc -= enc.yzww * kEncodeBit;
    return enc;
#endif
}